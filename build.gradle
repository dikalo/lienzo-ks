/*
 * Copyright (c) 2014,2015,2016 Ahome' Innovation Technologies. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'maven'
apply plugin: 'eclipse'

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.7

targetCompatibility = 1.7

version = '2.0.270'

repositories {
    mavenCentral()
    maven {
        url "${sonatype_snapshotURL}"
    }
}

sourceSets {
    main {
        resources {
             srcDir 'src/main/java'
        }
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
    }
}

dependencies {
    compile(group: 'com.arcbees.analytics', name: 'universal-analytics', version: '2.2')
    compile(group: 'com.ahome-it', name: 'lienzo-core', version: '2.0.270-RELEASE')
    providedCompile(group: 'com.google.gwt', name: 'gwt-dev', version: '2.7.0')
    providedCompile(group: 'com.google.gwt', name: 'gwt-user', version: '2.7.0')
    providedCompile(fileTree(dir: 'war/WEB-INF/lib', include: '*.jar'))
    providedCompile(group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0')
}

task copySrc(type: Copy) {
    from 'src/main/java'
    into 'build/classes/main'
}

task('gwtCompileWebApp', dependsOn: classes, type: JavaExec) {
    ext.buildDir = "${project.buildDir}/webapp"
    inputs.source sourceSets.main.java.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir
    outputs.upToDateSpec = new org.gradle.api.specs.AndSpec()
    doFirst {
        file(buildDir).mkdirs()
    }
    main = 'com.google.gwt.dev.Compiler'
    classpath {
        [sourceSets.main.java.srcDirs, sourceSets.main.output.classesDir, sourceSets.main.compileClasspath]
    }
    args =
            [
                'com.ait.lienzo.ks.LienzoKS',
                '-war',
                buildDir,
                '-logLevel',
                'INFO',
                '-style',
                'OBF',
                '-localWorkers',
                '4',
                '-XenableClosureCompiler',
            ]
    maxHeapSize = '2048M'
    minHeapSize = '2048M'
}

war.dependsOn(gwtCompileWebApp,copySrc)
war {
    manifest {
        attributes 'Implementation-Title': 'Lienzo-KS', 'Implementation-Version': version
    }
    from('war', gwtCompileWebApp.buildDir)
    webInf { from 'war/WEB-INF' }
    webXml = file('war/WEB-INF/web.xml')
    archiveName 'lienzo-ks.war'
}